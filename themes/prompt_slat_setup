# General style inspired by
# http://aperiodic.net/phil/prompt/
# 
# Untracked files check for git repositories from
# http://briancarper.net/blog/570/
#
# Git branch status from
# http://sebastiancelis.com/2009/11/16/zsh-prompt-git-users/

+vi-git-logo () {
	hook_com[vcs]=±
}

git-branch-status () {
	local st="$(git status 2>/dev/null)"
	if [[ -n "$st" ]]; then
		local -a arr
		arr=(${(f)st})
		if [[ $arr[2] =~ 'Your branch is' ]]; then
			if [[ $arr[2] =~ ' ahead ' ]]; then
				echo '%F{green}⬆'
			elif [[ $arr[2] =~ ' behind ' ]]; then
				echo '%F{yellow}⬇'
			elif [[ $arr[2] =~ ' diverged ' ]]; then
				echo '%F{red}⇅'
			fi
		fi
	fi
}

prompt_slat_precmd () {
	setopt noxtrace noksharrays localoptions

	local termwidth
	(( termwidth = ${COLUMNS} - 1 ))

	pr_pwdlen=""
	fillbar=""

	local promptsize=${#${(%):-┏┫%n┃%m┣┫┣┓}}
	local pwdsize=${#${(%):-%~}}

	if [[ "$promptsize + $pwdsize" -gt $termwidth ]]; then
		((pr_pwdlen=$termwidth - $promptsize))
	else
		fillbar="\${(l.(($termwidth - ($promptsize + $pwdsize)))..━.)}"
	fi

	# check for untracked files in git repositories
	if [[ -z $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then\
		zstyle ':vcs_info:git*' formats "%F{magenta}%s%F{blue}╻$(git-branch-status)\
%F{cyan}%b%c%u%F{blue}╻"
	else
		zstyle ':vcs_info:git*' formats "%F{magenta}%s%F{blue}╻$(git-branch-status)\
%F{cyan}%b%c%u%F{red}●%F{blue}╻"
	fi
	vcs_info
}

prompt_slat_setup () {

	autoload -Uz add-zsh-hook
	autoload -Uz colors && colors
	autoload -Uz vcs_info

	zstyle ':vcs_info:*' enable git
	zstyle ':vcs_info:git+set-message:*' hooks git-logo
	zstyle ':vcs_info:*' stagedstr '%F{green}●'
	zstyle ':vcs_info:*' unstagedstr '%F{yellow}●'
	zstyle ':vcs_info:*' check-for-changes true

	PROMPT='
%F{blue}┏┫%(!.%S%F{red}.%F{green})%n%s%F{blue}┃%F{cyan}%m\
%F{blue}┣${(e)fillbar}┫%F{yellow}%$pr_pwdlen<…<%~%<<%F{blue}┣\
┓\

┗┓${vcs_info_msg_0_}%(2L.%F{yellow}%L%F{blue}╻.)%(?..%F{red}%?%F{blue}╻)\
%(!.%F{red}.%F{cyan})%#%F{blue}┏❯%F{cyan}❯%F{green}❯%f '
	RPROMPT='%F{blue}┓%F{magenta}%D%F{blue}╻%F{magenta}%T%F{blue}┏┛%f'
	PS2='%F{blue} ┃%F{green}%_%F{blue}┣❯%F{cyan}❯%F{green}❯%f '

	prompt_opts=()
	prompt_opts+=cr
	prompt_opts+=subst
	prompt_opts+=percent
	add-zsh-hook precmd prompt_slat_precmd
}

prompt_slat_setup "$@"
